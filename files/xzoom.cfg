//    2-step-zoom script "xzoom" by X35

if (!= $xzoominstalled 1) [
	alias step1sens 2
	alias step2sens 0.4

	alias step1fov 50
	alias step2fov 30

	alias xzoombindbutton "MOUSE2"

	newgui temp [
		guitext "^f1This GUI will install the 2-step-zoom script 'xzoom' by X35 to your Sauerbraten."
		guibar
		guilist [
			guilist [
				guitext "^f7FOV for step 1: "
				guitext "^f7FOV for step 2: "
			]
			guibar
			guilist [
				guifield step1fov 5
				guifield step2fov 5
			]
		]
		guibar
		guilist [
			guilist [
				guitext "^f7sensitivity for step 1: "
				guitext "^f7sensitivity for step 2: "
			]
			guibar
			guilist [
				guifield step1sens 5
				guifield step2sens 5
			]
		]
		guibar
		guilist [
			guitext "^f7button to bind xzoom to: "
			guibar
			guifield xzoombindbutton 15
		]
		guibar
		guibutton "^f0save" [
			cleargui
			bind $xzoombindbutton [xzoom]
			alias xzoominstalled 1
		]
	] "xzoom installer."
	showgui temp
]

alias normalsens $sensitivity
alias normalfov $fov

xsetfov = [
	alias endfov $arg1
	continue_zoom $fov	
]


setsens = [
	alias newsens $arg1	
	if (< $newsens 0.4) [
		$newsens = 0.4
	]
	sensitivity $newsens
]

continue_zoom = [
	alias oldfov $arg1
	fov $oldfov
	if (> $oldfov $endfov) [
		sleep 1 [
			alias newfov ( - $oldfov 2 )
			(continue_zoom $newfov)
		]
	]
	if (< $oldfov $endfov) [
		sleep 1 [
			alias newfov ( + $oldfov 2 )
			(continue_zoom $newfov)
		]
	]
]


xzoom = [
	xsetfov $step1fov
	setsens $step1sens
	bind MOUSE2 [zoomstep2]
	bind MOUSE1 [attack; zoomstep3]
]


zoomstep2 = [
	xsetfov $step2fov
	setsens $step2sens
	bind MOUSE1 [attack; zoomstep3]
	bind MOUSE2 [zoomstep3]
]


zoomstep3 = [
	xsetfov $normalfov
	setsens $normalsens
	bind MOUSE1 [attack]
	bind MOUSE2 [xzoom]
]
