//                                       \\
//           FUNCTIONS LIBRARY           \\
//                                       \\
//          DEVELOPED BY SNOUTY          \\
//                                       \\
//             www.snouty.ch             \\
//                                       \\


//------------USEFUL FUNCTIONS-----------\\
listallclients = [ // A separate function for a list of all clients, because "(listclients)" returns all clients except me
	result (concatword (getclientnum) " " (listclients));
]


//--------ARE THE TEAMS BALANCED?--------\\
teamgamemodes = "11 12 13 14 15 16 18 19"; // Gamemodes with two teams, not possible to create another team like in instagib team, get the gamemode number with "/echo (getmode)"

checkmode = [ // Function to find out, if the current gamemode one of the $teamgamemodes is (constant teamnames enabled, "good" and "evil")
	returnvalue = 0;
	looplist i $teamgamemodes [
		if (= $i (getmode)) [
			returnvalue = 1;
		]
	]
	result $returnvalue;
]

isbalanced = [ // Small function which returns 1, when the teams are balanced and returns 0, when they arent, use "/echo (isbalanced)"
	if (checkmode) [
		difference = 0;
		looplist i (listallclients) [
			if (!= (isspectator $i) 1) [
				if (strcmp (getclientteam $i) "good") [
					difference = (+ $difference 1);
				] [
					difference = (- $difference 1);
				]
			]
		]
		result (&& (< $difference 2) (> $difference -2));
	]
]


//----WHO IS THE MASTER / ADMIN HERE?----\\
getmaster = [ // Function to get masters clientnum, "/echo (getmaster)" to get it, get masters name with "/echo (getclientname (getmaster))"
	returnvalue = "";
	looplist i (listallclients) [
		if (ismaster $i) [
			returnvalue = $i;
		]
	]
	result $returnvalue;
]

getadmin = [ // Function to get admins clientnum, "/echo (getadmin)" to get it, get admins name with "/echo (getclientname (getadmin))"
	returnvalue = "";
	looplist i (listallclients) [
		if (isadmin $i) [
			returnvalue = $i;
		]
	]
	result $returnvalue;
]


//-------------COLOR COMMANDS------------\\
result_colors = "green blue yellow red black purple orange white";

loop i 8 [ // Creates 8 functions for each color, use like "/echo (concatword "This color is " (green) "green.")", you don't have to write (result "^f2")
	[@(at $result_colors $i)] = [
		result (concatword "^f" @i);
	];
]


//-------LIST ALL CLIENTS IN A TEAM------\\
listclientsteam = [ // Function to list all clients in the team, which you select as parameter, returns a string like (listclients)
	returnvalue = "";
	looplist i (listallclients) [
		if (strcmp (getclientteam $i) $arg1) [
			returnvalue = (concatword $returnvalue $i " ");
		]
	]
	result $returnvalue;
]

listspectators = [ // Same like "/listclientsteam", but with spectators
	returnvalue = "";
	looplist i (listallclients) [
		if (isspectator $i) [
			returnvalue = (concatword $returnvalue $i " ");
		]
	]
	result $returnvalue;
]

//                                       \\
//                CREDITS                \\
//                                       \\
//      YOU CAN USE THIS FUNCTIONS,      \\
//    BUT BRING UP WHO DEVELOPED THEM    \\
//                                       \\
//            IF YOU USE THEM,           \\
//     DONT'T FORGET TO REDEFINE THEM    \\
//            IN YOUR SCRIPT!            \\
//                                       \\
//                 SNOUTY                \\
//                                       \\
//             www.snouty.ch             \\
//                                       \\