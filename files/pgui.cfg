
bind F3 [alias clientpointer 0; showgui tcclients];

newgui tcclients [
	alias clientlist (concat (listclients) (getclientnum (getname)));
	guilist [
		guilist [
			guilist [
				guislider clientpointer 0 (- (listlen $clientlist) 1);
				guilist [
					guistayopen [
						loop i (listlen $clientlist) [
							alias tmpcid (at $clientlist (- (listlen $clientlist) (+ 1 $i)));
							guibutton (concat (getclientname $tmpcid) $tmpcid) (concat clientpointer = (- (listlen $clientlist) (+ 1 $i))) ;
						]	
					]
				]
			]
			guibar;
		]
		guibar;
		guilist [
			guitext "Easy2Use Clientgui";
			alias gpcid (at $clientlist $clientpointer);
			guitext (concat (getclientname $gpcid) $gpcid);
			guibar ;
			guitext "TC-Mod Commands";
			guibutton "stats" (concat say #stats $gpcid);

			guibar ;
			guitext "HoP-Mod Commands";
			guibutton "names" (concat say #names $gpcid);
			guibutton "stats" (concat say #stats $gpcid);
			guibutton "mute" (concat say #mute $gpcid);
			guibutton "unmute" (concat say #unmute $gpcid);
			guibutton "givemaster" (concat say #givemaster $gpcid);
			guibar ;

			guitext "Default Commands";
			guibutton "kick" (concat kick $gpcid);
			guibutton "spect" (concat spectator 1 $gpcid);
			guibutton "unspect" (concat spectator 0 $gpcid);
			guibutton "goto" (concat goto $gpcid);
			guibutton "follow" (concat follow $gpcid);
			guibutton "team" menusetteam;
		]
	]	
]
menusetteam = [
	inputcommand "" [setteam $gpcid $commandbuf] "[teamname]"
]