// filez (based on: nyne's music player & somedude's save-brush-gui)
// 9/29/2014 :	
//		first "release"
//		new features : 	
//			GUIs
//			read/edit texts
//			play music
//			open folders
//			add zip files
// 9/29/2014 (still) :	
//		further work
//		new features :	
//			view images
//			play maps
//			code cleanups
//								
//***************************************************************************

createine = [ if (! (getalias $arg1)) [$arg1 = $arg2]]
createine _path "quality_edition"
createine _musicvol $musicvol
createine _muted 0
createine _musiconly 0
createine _notepadfile ""
createine _imgscale 1
createine _track = ""
createine _img = ""
createine _noautotab 0

playasong = [
	local _playlist
	loopfiles f "" "" [
		if (> (indexof [.mid .mp3 .ogg .wav .wma] (substr $f (- (strlen $f) 4))) -1) [
			_playlist = (concat (getalias _playlist) [[@@f]])
		]
	]
	_track = (at $_playlist (rnd (listlen $_playlist)))
	music [../quality_edition/music/@_track] playasong
	if (> $musicvol 1) [echo "^f7~ FileExplorer: playing song ^f6" $_track]
]

_getfiletype = [
	_gfti = (strlen $arg1)
	while [(&& $_gfti (! (=s (substr $arg1 (- $_gfti 1) 1) ".")))] [ _gfti = (- $_gfti 1)]
	if (> $_gfti 0) [result (substr $arg1 $_gfti)] [result "folder"]
]

_loadfiles = [
	if $_noautotab guinoautotab
	guistrut 1
	_files = ""
	if (_musiconly) [
		loopfiles f $_path "" [if (> (indexof [.mp3 .ogg] (substr $f (- (strlen $f) 4))) -1) [
			_files = (concat (getalias _files) [[@@f]])
			]
		]
	] [
		loopfiles f $_path "" [
			_files = (concat (getalias _files) [[@@f]])
		]
	]
	_files = (sortlist $_files x y [<=s $x $y])
	looplist f $_files [
		cond (> (indexof [.mp3 .ogg] (substr $f (- (strlen $f) 4))) -1) [		// Detect MP3 & OGG Files
			guibutton (concat "Play: " $f) [
				music (concatword "../" $_path "/" @f) playasong
				echo (concat "^f7~ FileExplorer: playing music^f6" @f)
				showgui musicx
			]
		] (> (indexof [.zip .pak] (substr $f (- (strlen $f) 4))) -1) [			// Detect ZIP & PAK Files
			guibutton (concat "Addzip: " $f) [
				addzip (concatword $_path "/" @f)
				echo (concat "^f7~ FileExplorer: added zip^f6" @f)
			]
		] (> (indexof [.cfg .txt] (substr $f (- (strlen $f) 4))) -1) [			// Detect CFG & TXT Files
			guibutton (concat "Open: " $f) [
				_notepadfile = (concatword $_path "/" @f)
				echo (concat "^f7~ FileExplorer: working on^f6" @f)
				showgui notepad
			]
		] (=s (_getfiletype $f) "folder") [										// Detect Folders
			guibutton (concat "Folder: " $f) [
				_path = (concatword $_path "/" @f)
			]
		] (> (indexof [.jpg .png] (substr $f (- (strlen $f) 4))) -1) [			// Detect JPG & PNG Files
			guibutton (concat "View: " $f) [
				_img = @f
				showgui imgx
			]
		] (> (indexof [.ogz] (substr $f (- (strlen $f) 4))) -1) [				// Detect OGZ Files
			guibutton (concat "Map: " $f) [
				map (concatword "../" $_path "/" @f)
			]
		]
	]
]

newgui filex [
	guistayopen [
		guilist [
			guitext "Set directory " "info.jpg"
			guifield _path -68
		]
		guitext (concat "Current Path: @" $_path) "info.jpg"
		_loadfiles
	]
] "FileExplorer"

showtexteditor = [
	guinoautotab [
		guieditor $arg1 $arg2 $arg3
		textinit $arg1 $arg1
		guistayopen [
			guilist [
				guibutton "load" [textfocus @arg1; textload @arg1]
				guistrut 1
				guibutton "save" [textfocus @arg1; textsave @arg1]
				guistrut 1
				guibutton "exec" [textfocus @arg1; textexec]
				guistrut 1
				guibutton "copy" [textfocus @arg1; textcopy]
				guistrut 1
				guibutton "paste" [textfocus @arg1; textpaste]
				guistrut 1
				guibutton "select" [textfocus @arg1; textselectall]
				guistrut 1
				guibutton "clear" [textfocus @arg1; textclear]
				guistrut 1
				guibutton "^f7<- Back" [ showgui filex ]
			]
		]
	]
]

newgui notepad [
	guitext _notepadfile
	showfileeditor $_notepadfile -80 20
] "Notepad"

newgui musicx [
	guistayopen [
		guitext (concat "^f7now playing:" $_track)
		if (> $musicvol 1) [
			guitext (concatword "^f7" (loopconcatword i (rnd 18) [ result ":" ])) 0
		]
		guistrut 1
		guitext "Music Volume:"
		guislider musicvol
		guistrut 1
		guitext "Sound Volume:"
		guislider soundvol
		guistrut 1
		guilist [
			if $_muted [
				guibutton "Mute" [_muted = 0; musicvol $_musicvol] checkbox_on
			] [
			guibutton "Mute" [_muted = 1; _musicvol = $musicvol; musicvol 1] checkbox_off
			]
		]
		guistrut 1
		guibutton "^f7Back" [showgui filex]
	]
] "Music"

newgui imgx [
	guistayopen [
		guinoautotab [
			guitext (concat "Viewing Image:" $_img)
			guistrut 1
			guiimage (concatword $_path "/" $_img) [] $_imgscale 
			guistrut 1
			guislider _imgscale 1 10
			guistrut 1
			guibutton "^f7Back" [ showgui filex ]
		]
	]
] "Image"