// ----------------------------------------------------
// Screenshot Tool, v1.3
// (C) Chaos, ZLIB license
// http://www.opensource.org/licenses/zlib-license.php
// ----------------------------------------------------

// Defaults:
createine = [ if (=s (getalias $arg1) "") [ $arg1 = $arg2 ] ] 
createine ss_opts 0
createine ss_extid 0
createine ss_ext ".png"
createine ss_name "screen"
createine ss_dir "screenshots"
ss_viewnum = 1

// Key Bind:
bind "M" [ showgui screens ]

// Miscellaneous Functions:
ss_modename = [ result (? (< (getmode) 20) (concatword (at (concat "sp" "dmsp" "demo" $modenames) (+ (getmode) 3)) "_") "main") ]
ss_isfilevalid = [ result (>= (max (strstr $arg1 ".png") (strstr $arg1 ".bmp") (strstr $arg1 ".tga"))) ]
ss_filelist = [
	slist = "";
	loopfiles f $ss_dir "" [ if (ss_isfilevalid $f) [ slist = (concatword $slist " ^"" $f "^"")) ] ]
	result $slist
]
ss_duelname = [
	nameslist = "";
	looplist i (listclients 1) [ nameslist = (concat $nameslist (? (isspectator $i) "" (getclientname $i))) ]
	result (? (&& (= (listlen $nameslist) 2) $arg1) (concatword "_" (at $nameslist 0) "-vs-" (at $nameslist 1))))
]
ss_getname = [
	repnum = 0;
	looplist i (ss_filelist) [ repnum = (+ $repnum (&& (> (strstr $i $arg1) -1) (> (strstr $i $arg2) -1))) ]
	result (concatword $arg1 (? $repnum (concatword "_" $repnum)))	
]

// GUI Menu:
newgui screens [
	guistayopen [
		guititle "Screenshot Manager"
		guibar
		guialign 0 [
			guilist [
				guititle "Options"
				guibar
				guibitfield "Hide Hudgun" ss_opts 1
				guibitfield "Hide Stats" ss_opts 2
				guibitfield "Hide Hud" ss_opts 4
				guibar
				guibitfield "Enable Duelscreens" ss_opts 8
				guibar
				guibitfield "Auto-Show GUI" ss_opts 16
				guibar
				guilist [
					guitext "Bind: " 0 
					newbinds = (searchbinds "screen")
					guikeyfield newbinds -8 [
						looplist i (searchbinds "screen") [ bind $i "" ]
						looplist i $newbinds [ bind $i "screen" ]
					]
				]
				guibar
				guilist [
					guitext "Directory: " 0
					guifield ss_dir -15 [ ss_viewnum = 1 ]
				]
				guibar
				guitext "Extension:" 0
				guiradio "png" ss_extid 0 [ ss_ext = ".png" ]
				guiradio "bmp" ss_extid 1 [ ss_ext = ".bmp" ]
				guiradio "tga" ss_extid 2 [ ss_ext = ".tga" ]
			]
			guibar
			guilist [
				guititle "Quick View"
				guibar
				if (>= (indexof (ss_filelist) $ss_name)) [
					guiimage (concatword $ss_dir "/" $ss_name) 0 8 
				] [
					guistrut 1
					guialign 0 [ guitext "^f4N/A" 0 ]
					guistrut 1 
				]
			]
		]
		guitab "Viewer"
		if (> (listlen (ss_filelist))) [
			guititle (format "%1 of %2" $ss_viewnum (listlen (ss_filelist)))
			guislider ss_viewnum 1 (listlen (ss_filelist))
			guibar
			guialign 0 [
				guibutton "^f7Prev" [ ss_viewnum = (- $ss_viewnum (> $ss_viewnum 1)) ] 0 
				guibar
				guilist [
					guialign 0 [
						guiimage (concatword $ss_dir "/" (at (ss_filelist) (- $ss_viewnum 1))) 0 8 
					]
					guitext (concatword "Name:^f7 " (at (ss_filelist) (- $ss_viewnum 1)))
				]
				guibar
				guibutton "^f7Next" [ ss_viewnum = (+ $ss_viewnum (< $ss_viewnum (listlen (ss_filelist)))) ] 0
			] 
		] [
			guistrut 1
			guialign 0 [ 
				guitext "^f4No Images" 0 
			]
		]
	]
] "Screens"

// Screenshot Command:
screen = [
	do [
		ss_name = (ss_getname (concatword (ss_modename) (mapname) (ss_duelname (& $ss_opts 8))) $ss_ext)
		hudgun (! (& $ss_opts 1))
		hidestats (> (& $ss_opts 2)) 
		hidehud (> (& $ss_opts 4))
		sleep 50 [ screenshot (concatword $ss_dir "/" $ss_name (? $ss_extid $ss_ext)) ]	
		sleep 100 [
			hudgun @@($hudgun)
			hidestats @@($hidestats)
			hidehud @@($hidehud)
		]
		sleep 100 [ 
			ss_name = (concatword $ss_name $ss_ext) 
			if (& $ss_opts 16) [ showgui screens ]
		]
	]
	echo "^f0* Screenshot Taken *"
]