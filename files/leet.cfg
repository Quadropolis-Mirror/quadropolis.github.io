newgui "Leet-Translator" [
	guititle "Leet-Translator"
	guibar
		guitext "Set the width of the field: " 0
		guislider "width_of_field" "1" "128" [translate = (substr [@translate] 0 $width_of_field)]
	guibar
	guilist [
		guitext "Your text: " 0
		guifield "translate" $width_of_field "mustblink = 1; blink"
		guistayopen [
			guibar
			guibutton [@translate_button_text] [mustblink = 0; checknumber = 0; number = 0; stelle = 0; replace_string]
		]
	]
	guibar
	guitext "   ^f5Your leeted text:" 0
	guitext [@(result "^f7")@translate] 0
	guibar
	guilist [
		guistayopen [
			guibutton "^f0Say it!" [method = 1; check_empty] "snoutx10k"
			guibar
			guibutton "^f1Teamchat it!" [method = 0; check_empty] "snoutx10k_blue"
			guibar
			guibutton "^f3Close this window" [cleargui] "exit"
		]
	]
] "Leet-Translator"

newgui "warning" [
	guitext "Your translate-field is empty." 0
	guitext "Type a text to translate it." 0
	guibar
	guialign 0 [
	guibutton "OK" [showgui "Leet-Translator"] "exit"
	]
] "^f3Warning!"

check_empty = [if (strcmp [@translate] "") [showgui "warning"] [if $method [say [@translate]] [sayteam [@translate]] ]]

char = "O o I i Z z E e A a S s G g T t"
translate_button_text = "^f7Translate"
width_of_field = "64"

replace_string = [loop x 16 [
	translate = (strreplace $translate (substr [@char] $stelle 1) $number)
	stelle = (+ $stelle 2)
	checknumber = (+ $checknumber 1)
	if (mod $checknumber 2) [] [number = (+ $number 1)]
	]
]

blink = [if (= $mustblink 1) [translate_button_text = "^f7Translate"; sleep 500 [translate_button_text = "^f3Translate"; sleep 500 [blink]]] [translate_button_text = "^f7Translate"]]

bind "F1" [showgui "Leet-Translator"]
editbind "F1" [showgui "Leet-Translator"]