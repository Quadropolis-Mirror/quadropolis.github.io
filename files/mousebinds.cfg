// ----------------------------------------------------
// MouseBinds Manager, v1.0
// (C) Chaos, ZLIB license
// sauerchaos@gmail.com
// http://www.opensource.org/licenses/zlib-license.php
// ---------------------------------------------------- 

_mbinds_type = ""
_mbinds_var = 1

newgui _mbinds_gui [
	guistayopen [
		guititle "Mouse-Binds Manager"
		guibar
		guialign 0 [
			guitext "^f2Type:" 0
			guistrut 2
			guibutton (concatword (? (=s $_mbinds_type "") "^f0") "normal") [ 
				_mbinds_type = ""
				_mbinds_var = 1
			] 0
			guibar
			guibutton (concatword (? (=s $_mbinds_type "edit") "^f0") "edit") [ 
				_mbinds_type = "edit"
				_mbinds_var = 1
			] 0
			guibar
			guibutton (concatword (? (=s $_mbinds_type "spec") "^f0") "spectator") [
				_mbinds_type = "spec"
				_mbinds_var = 1
			] 0
		]
		guibar
		guilist [ 
			guialign 0 [
				loop i 8 [  
					guitext (concatword "^f2MOUSE" (+ $i 1)) 0
				]
			]
			guibar 
			guialign 0 [
				loop i 8 [ 
					if $_mbinds_var [
						guitext (concatword "^f7" (do (concatword "get" $_mbinds_type "bind MOUSE" (+ $i 1)))) 0 
					] [
						guilist [
							guifield _mbinds_m@i -35
							guistrut 1
							guibutton "" [ _mbinds_m@i = "" ] "exit"
						]
					]
				]
			]
		]
		guibar
		guialign 1 [ 
			if $_mbinds_var [
				guibutton "Edit" [
					loop i 8 [
						[_mbinds_m@i] = (do (concatword "get" $_mbinds_type "bind MOUSE" (+ $i 1))) 
					]
					_mbinds_var = 0 
				] 0
			] [
				guibutton "Apply" [
					loop i 8 [
						do [
							(concatword $_mbinds_type "bind") (concatword "MOUSE" (+ $i 1)) $_mbinds_m@i 
						]
					]
					_mbinds_var = 1
				] 0
				guibar
				guibutton "Reset" [
					loop i 8 [
						[_mbinds_m@i] = (do (concatword "get" $_mbinds_type "bind MOUSE" (+ $i 1))) 
					]
				] 0
				guibar
				guibutton "Cancel" [ _mbinds_var = 1 ] 0
			]
		]
	]
	guitab "Reference" 
	guititle "Reference"
	guibar
	guialign 0 [
		guibar
		guilist [
			loop i 5 [ 
				guitext (concatword "^f2MOUSE" (+ $i 1)) 0 
				if (!= $i 4) [ guibar ]
			]
		]
		guibar
		guilist [
			guitext "^f7Left Click" 0
			guibar
			guitext "^f7Right Click" 0
			guibar
			guitext "^f7Middle Mouse" 0
			guibar
			guitext "^f7Scroll Up" 0
			guibar
			guitext "^f7Scroll Down" 0
		]
		guibar
		guibar
		guilist [
			guitext "^f2MOUSE6" 0
			guibar
			guitext "^f2MOUSE7" 0
			guibar
			guitext "^f2MOUSE8" 0
		]
		guibar
		guilist [
			guitext "^f7Extra" 0
			guibar
			guitext "^f7Extra" 0
			guibar
			guitext "^f7Extra" 0
		]
		guibar
	]
	guibar
	guitab "About"
	guititle "About"
	guibar
	guitextbox "MouseBinds Manager, v1.0^n(C) Chaos, ZLIB license^nsauerchaos@gmail.com^nhttp://www.opensource.org/licenses/zlib-license.php" 50
	guibar
	guitextbox "^f7Visit ^f1http://Chaos95.net^f7 to see all of your favorite scripts in one place." 50
] "MouseBinds"

mousebinds = [ showgui _mbinds_gui ]