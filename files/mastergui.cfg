alias mguigamemode 0;
alias mguisgal 0; // screenshot gallery. shows screens with messed up colours, caused by the sdl-lib. the developers and me cant change this. set value to 1 to enable.
newgui mastergui [
	guititle "MASTER - GUI";
		guilist [
			guilist [
				guibutton "get master" [setmaster 1]
				guibutton "leave master" [setmaster 0]
				if (= 1 (isspectator (getclientnum (getname)))) [
					guibutton "unspect me" [spectator 0];
				] [
					guibutton "spect me" [spectator 1];
				]
				guibutton "spect all" [spectator 1; loop i (listlen (listclients)) [spectator 1 (at (listclients) $i);]]
				guibutton "unspect all" [spectator 0; loop i (listlen (listclients)) [spectator 0 (at (listclients) $i);]]
			]
			guibar
			guilist [
				guitext "mastermode";
				guibutton "0 = open" [mastermode 0; echo "mastermode is now open"];
				guibutton "1 = veto" [mastermode 1; echo "mastermode is veto, you can decide maps and modes without a vote"];
				guibutton "2 = locked" [mastermode 2; echo "mastermode is locked, joining people will become spectator"];
				guibutton "3 = private" [mastermode 3; echo "mastermode is now private, noone is able to connect"];
			]
			guibar
			guilist [
				guitext "assistents";
				guibutton "playerboard" [showgui mguiteams];
				guibutton "clanwar" [showgui mguiclanwar];
				guibutton "one vs one" [showgui mguiovo];
			]
		]
		guibar
		guititle "Vote Modes";
		guilist [
			guilist [
				guitext "Deathmatch";
				guibutton "ffa"	    		"mode 0; showgui maps"
				guibutton "instagib"		"mode 3; showgui maps"
				guibutton "efficiency"		"mode 5; showgui maps"
				guibutton "tactics"		"mode 7; showgui maps"
			]
			guibar
			guilist [
				guitext "Team Deathmatch";
				guibutton "teamplay"		"mode 2; showgui maps"
				guibutton "instagib team"	"mode 4; showgui maps"
				guibutton "efficiency team"	"mode 6; showgui maps"
				guibutton "tactics team"	"mode 8; showgui maps"
			]
			guibar
			guilist [
				guitext "capture & ctf";
				guibutton "capture"		"mode  9; showgui capturemaps"
				guibutton "regen capture"	"mode 10; showgui capturemaps"
				guibutton "ctf"			"mode 11; showgui ctfmaps"
				guibutton "insta ctf"		"mode 12; showgui ctfmaps"
			]
		]
		guibar
		guititle "Additional";
	guilist [
		guilist [
			guitext "Screenshots";
			guibutton "with hud" "sleep (div 2000 (getfps)) [mguiscreen 0]";
			guibutton "without hud" "sleep (div 2000 (getfps)) [mguiscreen 1]";
			if (= $mguisgal 1) [
				guibutton "gallery" "showgui mguigallery";
			]
		]
		guibar
		guilist [
			guitext "dunno";
		]
		guibar
		guilist [
			guitext "Rename";
			guilist [
				guifield mguiname1 15; guibutton "" [name $mguiname1];
			]
			guilist [
				guifield mguiname2 15; guibutton "" [name $mguiname2];
			]
			guilist [
				guifield mguiname3 15; guibutton "" [name $mguiname3];
			]
		]
	]
]
	
newgui mguiteams [
	guistayopen [ 
		alias mguiclientlist (concat (listclients) (getclientnum (getname)));
		alias spectating (isspectator (getclientnum (getname)));
  		if (> (getmode) 10) [
  			alias mguilistteamgood "";
  			alias mguilistteamevil "";
  			alias mguispectlist "";
  			loop i (listlen $mguiclientlist) [
  				alias mguilpid (at $mguiclientlist $i);
  				if (= 1 (isspectator $mguilpid)) [
  					mguispectlist = (concat $mguispectlist $mguilpid)
  				] [
  					alias mguivarname (concatword "mguilistteam" (getclientteam $mguilpid));
  					$mguivarname = (concat (getalias $mguivarname) $mguilpid)
  				]
  			]
  			guilist [
  				guilist [
  					guitext "GOOD";
  					loop i (listlen $mguilistteamgood) [
  						alias mguilpid (at $mguilistteamgood $i);
  						guilist [
  							guiimage "packages/icons/exit.jpg" (concat kick $mguilpid)  0.5 0;
  							if (strcmp "good" (getteam)) [
  								guiimage "packages/icons/ironsnout_blue.jpg" (concat changeteam $mguilpid evil) 0.5 0;
  							] [
  								guiimage "packages/icons/ironsnout_red.jpg" (concat changeteam $mguilpid evil) 0.5 0;
  							] 
  							if ($spectating) [
  								guiimage "packages/icons/menu.jpg" (concat follow $mguilpid) 0.5 0;
  							]
			  				guibutton (getclientname $mguilpid) (concat spectator 1 $mguilpid) "spectator" ;
			  			]
			  		]
			  	]
			  	guibar;
			  	guilist [
			  		guitext "EVIL";
			  		loop i (listlen $mguilistteamevil) [
			  			alias mguilpid (at $mguilistteamevil $i);
			  			guilist [
			  				guiimage "packages/icons/exit.jpg" (concat kick $mguilpid)  0.5 0;
			  				if (strcmp "evil" (getteam)) [
			  					guiimage "packages/icons/ironsnout_blue.jpg" (concat changeteam $mguilpid good) 0.5 0;
			  				] [
			  					guiimage "packages/icons/ironsnout_red.jpg" (concat changeteam $mguilpid good) 0.5 0;
			  				]
			  				if ($spectating) [
			  					guiimage "packages/icons/menu.jpg" (concat follow $mguilpid) 0.5 0;
			  				]
			  				guibutton (getclientname $mguilpid) (concat spectator 1 $mguilpid) "spectator" ;
			  			]
			  		]
			  	]
			  ]
	   		guibar;
	   		guitext "Spectator";
			  loop i (listlen $mguispectlist) [
  				alias mguilpid (at $mguispectlist $i);
  				guilist [
  					guiimage "packages/icons/exit.jpg" (concat kick $mguilpid)  0.5 0;
  					guibutton (getclientname $mguilpid) (concat spectator 0 $mguilpid) "ironsnout" ;
  				]
  			]
  		] [
  	  		if (=	(isteammode) 1) [
    				alias mguiteamlist "";
     				loop i (listlen $mguiclientlist) [
    					alias mguilpid (at $mguiclientlist $i);
    					if (= 0 (isspectator $mguilpid)) [
    						if (= (strstr $mguiteamlist (getclientteam $mguilpid)) -1) [
    								mguiteamlist = (concat $mguiteamlist (getclientteam $mguilpid)));
    						]
    					]
    				] 
    				alias mguispectlist "";
     				loop i (listlen $mguiteamlist) [
    					alias (concatword mguilistteam (at $mguiteamlist $i)) "";
    				]
    				loop i (listlen $mguiclientlist) [
    					alias mguilpid (at $mguiclientlist $i);
    					if (= (isspectator $mguilpid) 1) [
    						mguispectlist = (concat $mguispectlist $mguilpid)
    					] [
    						alias mguiteamname (concatword mguilistteam (getclientteam $mguilpid));
    						$mguiteamname = (concat (getalias $mguiteamname) $mguilpid);
    					]
     				]
    	 			guilist [
    					loop i (listlen $mguiteamlist) [
    						guilist [
    							guitext (at $mguiteamlist $i);
    							alias mguiteamname (concatword mguilistteam (at $mguiteamlist $i));
  	   						loop j (listlen (getalias $mguiteamname)) [
    	 							alias mguilpid (at (getalias $mguiteamname) $j);
  	  							guilist [
  	   								guiimage "packages/icons/exit.jpg" (concat kick $mguilpid) 0.5 0;
  				  					guiimage "packages/icons/ironsnout.jpg" (concat alias mguicurrent $mguilpid ";" showgui mguisetteam;) 0.5 0;
  					   				if ($spectating) [
  					 	   				guiimage "packages/icons/menu.jpg" (concat follow $mguilpid) 0.5 0;
  							   		]
  				  					guibutton (getclientname $mguilpid) (concat spectator 1 $mguilpid) "spectator" ;
  				  				]
  				  			]
  				  		]
  				  		guibar;
  				  	]
  				  	guilist [
  				  		guitext "Spectator";
  				  		loop i (listlen $mguispectlist) [
  				  			alias mguilpid (at $mguispectlist $i);
  				  			guilist [
  				  				guiimage "packages/icons/exit.jpg" (concat kick $mguilpid) 0.5 0;
  				  				guibutton (getclientname $mguilpid) (concat spectator 0 $mguilpid) "ironsnout" ;
  				  			]
  				   		]
  	 			   	]
  	   			]
   		   	] [
				guilist [
					alias colsize 16;
					alias mguiplayerlist "";
					alias mguispectatorlist "";
					loop i (listlen $mguiclientlist) [
						alias mguilpid (at $mguiclientlist $i);
						if (= 1 (isspectator $mguilpid)) [
							mguispectatorlist = (concat $mguispectatorlist $mguilpid); 
						] [
							mguiplayerlist = (concat $mguiplayerlist $mguilpid); 
						]
					]
					mguiplayerlist = (concat $mguiplayerlist $mguispectatorlist);
					loop i (+ 1 (div (- (listlen $mguiplayerlist) 1) $colsize)) [
						guilist [
							loop j (min $colsize (- (listlen $mguiplayerlist) (* $colsize $i))) [
								guilist [
										alias mguilpid (at $mguiplayerlist (+ (* $i $colsize) $j));
					  				guiimage "packages/icons/exit.jpg" (concat kick $mguilpid)  0.5 0;
									if (= 0 (isspectator $mguilpid)) [
					  					if ($spectating) [
					  						guiimage "packages/icons/menu.jpg" (concat follow $mguilpid) 0.5 0;
					  					]
	  					  				guiimage "packages/icons/spectator.jpg" (concat spectator 1 $mguilpid) 0.5 0;
									] [
	  					  				guiimage "packages/icons/ironsnout.jpg" (concat spectator 0 $mguilpid) 0.5 0;
									]
									guitext (concat (getclientname $mguilpid) $mguilpid);
								]
							]
						]
						if (! $i (+ 1 div (- (listlen $mguiplayerlist) 1) $colsize) ) [
							guibar;
						]
	      				]
				]
			]
	 	]
	]		
]
alias mguiclientteam "";
newgui mguisetteam [
	guitext (getclientname $mguicurrent);
	guibar;
	alias mguiteamlist "";
	guilist [
		guilist [
			loop i (listlen $mguiclientlist) [
				alias mguilpid (at $mguiclientlist $i);
				guilist [
					if (= 0 (isspectator $mguilpid)) [
						if (= (strstr $mguiteamlist (getclientteam $mguilpid)) -1) [
							mguiteamlist = (concat $mguiteamlist (getclientteam $mguilpid)));
							guibutton (getclientteam $mguilpid) (concat changeteam $mguicurrent (getclientteam $mguilpid));
						]
					]
				]
			]
		]
		guibar
		guilist [
			guitext teamname
			guifield mguiclientteam 4 
			guibutton "set team" [changeteam $mguicurrent $mguiclientteam]
		]
	]
]
alias mguicwm 2;
alias mguimm 2;
alias mguicwmn;
alias mguicwtn "";
alias mguicwtngood "";
alias mguicwtnevil "";
newgui mguiclanwar [
	guilist [
		guilist [
			loop i 13 [
				if (= 1 (isteammode $i)) [
					guiradio (getmodename $i) mguicwm $i
				]
			]
		]
		guibar
		guilist [
			guiradio "mastermode 2" mguimm 2
			guiradio "mastermode 3" mguimm 3
			guilist [
				guitext "map "
				guifield mguicwmn 15 
				if (> $mguicwm 10) [
					if (= 1 (isinlist $mguicwmn $ctfmaps1 $ctfmaps2)) [
						guiimage "packages/icons/radio_on.jpg" "" 0.5 0;
					] [
						guiimage "packages/icons/exit.jpg" "" 0.5 0;
					]
				] [
					if (> $mguicwm 8) [
						if (= 1 (isinlist $mguicwmn $capturemaps1 $capturemaps2)) [
							guiimage "packages/icons/radio_on.jpg" "" 0.5 0;
						] [
							guiimage "packages/icons/exit.jpg" "" 0.5 0;
						]
					] [
						if (= 1 (isinlist $mguicwmn $mainmaps $moremaps1 $moremaps2 $moremaps3 $capturemaps1 $capturemaps2 $ctfmaps1 $ctfmaps2 $conceptmaps)) [
							guiimage "packages/icons/radio_on.jpg" "" 0.5 0;
						] [
							guiimage "packages/icons/exit.jpg" "" 0.5 0;
						]
					]
				]
			]
			guibar;
			guibutton teams "showgui mguicwteams";
		]
	]
]
newgui mguicwteams [
	if (> $mguicwm 10) [
		guistayopen [ 
			guilist [
				guilist [
					guitext "good";
					guifield mguicwtngood 20;
					alias mguitl $mguicwtngood;
					guibar;
					loop i (listlen $mguitl) [
						if (= 1 (strcmp "" (getclientname (at $mguitl $i)))) [	
							guibutton "invalid id" (concat echo id (at $mguitl $i) is wrong)  exit;
						] [
							guitext (getclientname (at $mguitl $i));
						]
					]
				]
				guibar;
				guilist [
					guitext "evil";
					guifield mguicwtnevil 20;
					alias mguitl $mguicwtnevil;
					guibar;
					loop i (listlen $mguitl) [
						if (= 1 (strcmp "" (getclientname (at $mguitl $i)))) [
							guibutton "invalid id" (concat echo id (at $mguitl $i) is wrong)  exit;
						] [
							guitext (getclientname (at $mguitl $i));
						]
					]
				]
			]
		]
		guibar;
		guibutton "start war" mguicwstart;
	] [
		guitext (concatword "teams (" (listlen $mguicwtn) "): ");
		guifield mguicwtn 40;
		guibar;
		if (> (listlen $mguicwtn) 1) [
			guilist [
				loop j (listlen $mguicwtn) [
					guilist [
						alias mguiltn (at $mguicwtn $j)
						guitext $mguiltn;
						alias mguiltln (concatword mguicwtl $j)
						guifield $mguiltln 20;
						alias mguitl (getalias $mguiltln);
						guibar;
						loop i (listlen $mguitl) [
							if (= 1 (strcmp "" (getclientname (at $mguitl $i)))) [
								guibutton "invalid id" (concat echo id (at $mguitl $i) is wrong)  exit;
							] [
								guitext (getclientname (at $mguitl $i));
							]
						]
					]
					guibar;
				]
			]
		]
		guibar;
		guibutton "start war" mguicwstart;
	]
]
mguicwstart = [
	mastermode $mguimm;
	spectator 1;
	loop i (listlen (listclients)) [
		spectator 1 (at (listclients) $i);
	]
	sleep 1000 [
		mode $mguicwm;
		map $mguicwmn;
		mguicwcountdownloop 12;
		sleep 3000 [
			if (> $mguicwm 10) [
				alias mguitl $mguicwtngood;
				loop i (listlen $mguitl) [
					changeteam (at $mguitl $i) good;
				]
				alias mguitl $mguicwtnevil;
				loop i (listlen $mguitl) [
					changeteam (at $mguitl $i) evil;
				]
				] [
				loop j (listlen $mguicwtn) [
					alias mguiltn (at $mguicwtn $j)
					alias mguiltln (concatword mguicwtl $j)
					alias mguitl (getalias $mguiltln);
					loop i (listlen $mguitl) [
						changeteam (at $mguitl $i) $mguiltn;
					]
				]
			]
		]
	]
]
mguicwcountdownloop = [
	say $arg1;
	if (= 1 (mguicheckstart $arg1)) [
		if (> $mguicwm 10) [
			alias mguitl $mguicwtngood;
			loop i (listlen $mguitl) [
				spectator 0 (at $mguitl $i);
			]
			alias mguitl $mguicwtnevil;
			loop i (listlen $mguitl) [
				spectator 0 (at $mguitl $i);
			]
		] [
			loop j (listlen $mguicwtn) [
				alias mguiltln (concatword mguicwtl $j)
				alias mguitl (getalias $mguiltln);
				loop i (listlen $mguitl) [
					spectator 0 (at $mguitl $i);
				]
			]
		]
		
	]
	if (> $arg1 0) [
		alias sleepwaittime (- $arg1 1);
		sleep 1000 [mguicwcountdownloop $sleepwaittime];
	]
]
mguicheckstart = [
	alias mguiretval 0;
	if (= $arg1 10) [
		if (= $mguicwm 9) [
			mguiretval = 1;
		] 
	] [
		if (= $arg1 5) [
			if (> $mguicwm 10) [
				mguiretval = 1;
			]
		] [
			if (= $arg1 0) [
				mguiretval = 1;
			]
		]
	]
	result $mguiretval;
]
changeteam = [
	if (= (getclientnum (getname)) $arg1) [
		team $arg2;
	] [
		setteam $arg1 $arg2;
	]
]
newgui mguigallery [
	loop i 10 [
		if $i [
			guitab $i;
		]
		alias mguisslp (mod (- (+ $mguissp 20) $i) 10);
		guitext $mguisslp;
		guiimage (getalias (concatword mguiss  $mguisslp)) "" 8
	]
]
isteammode = [
  alias retval 0;
  if (= $arg1 2) [
    retval = 1;
  ]
  if (= $arg1 4) [
    retval = 1;
  ]
  if (= $arg1 6) [
    retval = 1;
  ]
  if (= $arg1 8) [
    retval = 1;
  ]
  if (= $arg1 9) [
    retval = 1;
  ]
  if (= $arg1 10) [
    retval = 1;
  ]
  if (= $arg1 11) [
    retval = 1;
  ]
  if (= $arg1 12) [
    retval = 1;
  ]
  result $retval;
]
getmodename = [
	alias returnmode "sp";
	if (= $arg1 0 ) [
		returnmode = "ffa";
	]
	if (= $arg1 1) [
		returnmode = "coop";
	]
	if (= $arg1 2) [
		returnmode = "teamplay";
	]
	if (= $arg1 3) [
		returnmode = "insta";
	]
	if (= $arg1 4) [
		returnmode = "instateam";
	]
	if (= $arg1 5) [
		returnmode = "eff";
	]
	if (= $arg1 6) [
		returnmode = "effteam";
	]
	if (= $arg1 7) [
		returnmode = "tactic";
	]
	if (= $arg1 8) [
		returnmode = "tacticteam";
	]
	if (= $arg1 9) [
		returnmode = "capture";
	]
	if (= $arg1 10) [
		returnmode = "regen";
	]
	if (= $arg1 11) [
		returnmode = "ctf";
	]
	if (= $arg1 12) [
		returnmode = "ictf";
	]
	result $returnmode;
]
isinlist = [
	alias retval 0;
	alias varsel 2;
	while [= 0 (strcmp (getalias (concatword arg $varsel)) "")] [
		list = (getalias (concatword arg $varsel));
		loop i (listlen $list) [
			if (= 1 (strcmp $arg1 (at $list $i ))) [
				retval = 1;
			]
		]
		varsel = (+ $varsel 1);
	]
	result $retval;
]
loop i 10 [
	createine (concatword "mguiss" $i) data/cube.png;
]
createine mguissp 0;
mguiscreen = [
	if (strcmp (getalias "screenshotid") "") [
		alias screenshotid 0
	]
	screennname = (concatword "screens/" (getmodename) "-"(strreplace (mapname) "base\" "" ) "-" $screenshotid  ".bmp");
	if (= $arg1 1) [
		hidehud 1;
		hudgun 0;
		sleep (div 2000 (getfps)) [
			screenshot $screennname;
			echo $screennname; 
			hidehud 0;
			hudgun 1;
		]
	] [
		screenshot $screennname;
		echo $screennname; 
	]
	screenshotid = (+ $screenshotid 1);
	mguissp = (mod (+ 1 $mguissp) 10);
	alias mguissan (concatword mguiss $mguissp);
	$mguissan = $screennname;
]
createine = [
	if (= 1 (strcmp "" (getalias $arg1))) [
		alias $arg1 $arg2;
	]
]