createine = [if (=s (getalias $arg1) "") [$arg1 = $arg2]]

createine _fs_servers 5
looplist p "serv ip port" [
	loop i 15 [
		createine [_fs_@@p@i]
	]
]

newgui favserv [
	guititle "^f2Select your favorite server to connect"
	guibar
	loop i $_fs_servers [
		if (&& [!=s $_fs_serv@i ""] [!=s $_fs_ip@i ""]) [
			guibutton (concat "Connect to^f2" $_fs_serv@i) [
				connect $_fs_ip@i $_fs_port@i
			] cube
		]
	]
	guinoautotab [
		guitab "Config"
		guistayopen [
			guilist [
				guilist [
					guititle "^f2Server Name"
					guibar
					loop i $_fs_servers [
						guilist [
							guibutton "" [
								looplist p "serv ip port" [
									[_fs_@[p]@@@i] = ""
								]
							] exit
							guifield _fs_serv@i 20
						]
					]
				]
				guibar
				guilist [
					guititle "^f2Address"
					guibar
					loop i $_fs_servers [
						guifield [_fs_ip@i] 20 [
							if (=s $_fs_ip@i "") [
								_fs_ip@@i = (? (isconnected) (connectedip))
							]
						]
					]
				]
				guibar
				guilist [
					guititle "^f2Port"
					guibar
					loop i $_fs_servers [
						guifield [_fs_port@i] 5 [
							if (=s $_fs_port@i "") [
								_fs_port@@i = (? (isconnected) (connectedport) 28785)
							]
						]
					]
				]
			]
			guititle
			guilist [
				if (< $_fs_servers 15) [
					guibutton "Add Server" [
						_fs_servers = (min 15 (+ $_fs_servers 1))
					]
				]
				guispring
				if (> $_fs_servers 5) [
					guibutton "Remove Server" [
						_fs_servers = (max 5 (- $_fs_servers 1))
					]
				]
			]
		]
	]
] "Join Server"