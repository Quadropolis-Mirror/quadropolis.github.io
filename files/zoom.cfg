createine = [
	if (strcmp "" (getalias $arg1)) [
		alias $arg1 $arg2;
	]
]
alias chlist ""
loopfiles f "config/crosshair/" png [
	chlist = (concat $chlist (concatword $f ".png"));
]
alias sumcrosshair (- (listlen $chlist) 1);
newgui "Zoomconfig" [
	guititle "Zoom"
	guitext "Zoomkey"
	guifield zoomkey 10 [createzoombind];
	guicheckbox "zoom onrelease" zoomonrelease;
	guibar
	createzoombind

	guitext "normal fov"
	guifield standardfov 3 [if (> $standardfov 150) [standardfov = 150]; if (< $standardfov 10) [standardfov = 10]];
	guitext "default zoom fov"
	guifield standardzoomfov 3 [if (> $standardzoomfov 150) [standardzoomfov = 150]; if (< $standardzoomfov 10) [standardzoomfov = 10]; alias szfbu $standardzoomfov;];
	guibar
	guibutton "Sensitivity-Config"			 "showgui sensscaleconfig"	

	guitab "Zoomscroll"
	guititle "Scroll-Setup"
	guicheckbox "Store Zoom" storezoom;
	guitext "Step zooming in"
	guislider zoomscrollin 0 20;
	guitext "Step zooming out"
	guislider zoomscrollout 0 20;
	guibar
	guicheckbox "invert scroll" zoomscrollinvert

	

	guitab "Snipercrosshair"
	guititle "Crosshair-Setup"
	guicheckbox "special zoom-crosshair" zoomchactive;
	if (= $zoomchactive 1) [
		guibar;
		guislider crosshairid 0 $sumcrosshair;
		guibar;
		guitext (at $chlist $crosshairid);
		guilist [
			guilist [
				guitext "Zoomcrosshair"
				guiimage (concatword "config/crosshair/" (at $chlist $crosshairid)) "" 2;
			]
			guibar;
			guilist [
				guitext "FX Hit"
				guiimage (concatword "config/crosshairfx/" (at $chlist $crosshairid)) "" 2;
			]
		]
	]
	guibar
	guitext "crosshairsize - normal"
	guislider nozoomchs 0 50;
	guitext "crosshairsize - zoom"
	guislider zoomchs 0 50;

	guitab "Additional"
	guititle "Various Stuff"
	guicheckbox "Auto-Sniperweapons" ffaautosniperweapons;
	guibutton " Mouse2-Config"			 "showgui mouse2config"	
	guibutton " Scroll-Weaponselector"		"showgui ffascrollconfig"
	guicheckbox "Show boundingbox in zoom" showhitboxinzoom;			

]
newgui mouse2config [
	guititle "FFA"
	guiradio "nothing"		ffam2bind -1;
	guiradio "ffabindMouse2"	ffam2bind 0;
	guiradio "Jump"			ffam2bind 1;
	guicheckbox "pick up ammo at base" m2bindrepammo;

	guitab "FFA-Zoom"
	if (= $storezoom 1) [
		guitext "Because storezoom is active this option is locked."
	] [	
		guiradio "nothing"		ffazoomm2bind -1;
		guiradio "Zoomweapons"		ffazoomm2bind 0;
		guiradio "ffabindMouse2"	ffazoomm2bind 1;
		guiradio "Jump"			ffazoomm2bind 2;
		guiradio "Resetzoom"		ffazoomm2bind 3;
		guiradio "Sparkling Enemies"	ffazoomm2bind 4;
		guicheckbox "pick up ammo at base" m2bindrepammo;
	]

	guitab "Insta"
	guiradio "nothing"		instam2bind -1;
	guiradio "Jump"			instam2bind 0;
	guiradio "Toggle Weapon"	instam2bind 1;
	guiradio "Quickfist"		instam2bind 2;

	guitab "Insta-Zoom"
	if (= $storezoom 1) [
		guitext "Because storezoom is active this option is locked."
	] [
		guiradio "nothing"		instazoomm2bind -1;
		guiradio "Jump"			instazoomm2bind 0;
		guiradio "Resetzoom"		instazoomm2bind 1;
		guiradio "Sparkling Enemies"	instazoomm2bind 2;
	]
]
newgui sensscaleconfig [
	guicheckbox "More Settings" sensscalechange;
	if (= $sensscalechange 0) [
		guitext "sensitivityscale"
		guifield defaultsensscale 3 [sensitivityscale $defaultsensscale; ]
		guislider defaultsensscale 0 999 [sensitivityscale $defaultsensscale; ];
	] [
		guitext "SCALE - FFA"
		guifield ssffa 3 
		guislider ssffa 0 999;
		guitext "SCALE - FFA-ZOOM"
		guifield ssffazoom 3 
		guislider ssffazoom 0 999;
		guitext "SCALE - INSTA"
		guifield ssinsta 3 
		guislider ssinsta 0 999;
		guitext "SCALE - INSTA-ZOOM"
		guifield ssinstazoom 3 
		guislider ssinstazoom 0 999;
	]
]

newgui ffascrollconfig [
	guitext "Modeselector"
	guislider ffascrollmode 0 5;
	guitext (getscrollmodename)
	guibar

	if (= $ffascrollmode 0) [
	]
	if (= $ffascrollmode 1) [
		guitext "Number of weapons"
		guislider cwcounter 1 5;
		guibar
		guilist [
			guilist [
				loop i $cwcounter [
					guitext (concat "Weapon" $i);
				]
			]
			guibar
			guilist [
				loop i $cwcounter [
					alias cwvarname (concatword "cwid" $i);
					guifield $cwvarname 1 [ $cwvarname = (+ (getalias $cwvarname) 0); if (> (getalias $cwvarname) 6) [ $cwvarname = 0]];
				]
			]
		]
		guibar
		guilist [
			guilist [
				guitext "0 Fist"
				guitext "1 Shotgun"
				guitext "2 Machinegun"
				guitext "3 Rockets"
			]
			guibar
			guilist [
				guitext "4 Rifle"
				guitext "5 Grenades"
				guitext "6 Pistol"
			]
		]
	]
	if (= $ffascrollmode 2) [
		guilist [
			guilist [
				guitext "Weapon";
				guitext "Primary";
				guitext "Secondary";
				guitext "Alternative";
			]
			guibar
			guilist [
				guitext "UP";
				guifield wm41 1 [ wm41 = (+ $wm41 0); if (> $wm41 6) [ wm41 = 0]];
				guifield wm42 1 [ wm42 = (+ $wm42 0); if (> $wm42 6) [ wm42 = 0]];
				guifield wm43 1 [ wm43 = (+ $wm43 0); if (> $wm43 6) [ wm43 = 0]];
			]
			guibar
			guilist [
				guitext "DOWN";
				guifield wm51 1 [ wm51 = (+ $wm51 0); if (> $wm51 6) [ wm51 = 0]];
				guifield wm52 1 [ wm52 = (+ $wm52 0); if (> $wm52 6) [ wm52 = 0]];
				guifield wm53 1 [ wm53 = (+ $wm53 0); if (> $wm53 6) [ wm53 = 0]];
			]
		]
		guibar
		guilist [
			guilist [
				guitext "0 Fist"
				guitext "1 Shotgun"
				guitext "2 Machinegun"
				guitext "3 Rockets"
			]
			guibar
			guilist [
				guitext "4 Rifle"
				guitext "5 Grenades"
				guitext "6 Pistol"
			]
		]
	]
	if (= $ffascrollmode 3) [
		guilist [
			guilist [
				guitext "UP";
				guitext "DOWN";
			]
		guibar
			guilist [
				guifield swm4 1 [ swm4 = (+ $swm4 0); if (> $swm4 6) [ swm4 = 0]];
				guifield swm5 1 [ swm5 = (+ $swm5 0); if (> $swm5 6) [ swm5 = 0]];
			]
		]
		guibar
		guilist [
			guilist [
				guitext "0 Fist"
				guitext "1 Shotgun"
				guitext "2 Machinegun"
				guitext "3 Rockets"
			]
			guibar
			guilist [
				guitext "4 Rifle"
				guitext "5 Grenades"
				guitext "6 Pistol"
			]
		]
	]
	if (= $ffascrollmode 4) [
	]
	if (= $ffascrollmode 5) [
		guilist [
			guilist [
				guitext "UP"
				guislider cuwcounter 1 5;
				guibar
				guilist [
					guilist [
						loop i $cuwcounter [
							guitext (concat "Weapon" $i);
						]
					]
					guibar
					guilist [
						loop i $cuwcounter [
							alias cuwvarname (concatword "cuwid" $i);
							guifield $cuwvarname 1 [ $cuwvarname = (+ (getalias $cuwvarname) 0); if (> (getalias $cuwvarname) 6) [ $cuwvarname = 0]];
						]
					]
				]
			]
			guibar
			guilist [
				guitext "DOWN"
				guislider cdwcounter 1 5;
				guibar
				guilist [
					guilist [
						loop i $cdwcounter [
							guitext (concat "Weapon" $i);
						]
					]
					guibar
					guilist [
						loop i $cdwcounter [
							alias cdwvarname (concatword "cdwid" $i);
							guifield $cdwvarname 1 [ $cdwvarname = (+ (getalias $cdwvarname) 0); if (> (getalias $cdwvarname) 6) [ $cdwvarname = 0]];
						]
					]
				]
			]
		]
		guibar;
		guilist [
			guilist [
				guitext "0 Fist"
				guitext "1 Shotgun"
				guitext "2 Machinegun"
				guitext "3 Rockets"
			]
			guibar
			guilist [
				guitext "4 Rifle"
				guitext "5 Grenades"
				guitext "6 Pistol"
			]
		]
	]

]

// SETTINGS!!
createine "cwcounter"			5;
createine "cwid0"			1;
createine "cwid1"			2;
createine "cwid2"			3;
createine "cwid3"			4;
createine "cwid4"			5;
createine "cuwcounter"			5;
createine "cuwid0"			1;
createine "cuwid1"			2;
createine "cuwid2"			3;
createine "cuwid3"			4;
createine "cuwid4"			5;
createine "cdwcounter"			5;
createine "cdwid0"			1;
createine "cdwid1"			2;
createine "cdwid2"			3;
createine "cdwid3"			4;
createine "cdwid4"			5;
createine "defaultsensscale"		100;
createine "ffascrollmode"		0;
createine "sensscalechange"		0;
createine "ssffa"			100;
createine "ssffazoom"			100;
createine "ssinsta"			100;
createine "ssinstazoom"			100;
createine "swm4"			4;
createine "swm5"			5;
createine "wm41"			1;
createine "wm42"			2;
createine "wm43"			3;
createine "wm51"			4;
createine "wm52"			5;
createine "wm53"			6;
createine "ffam2bind"			0;
createine "ffazoomm2bind"		0;
createine "m2bindrepammo"		0;
createine "instam2bind"			0;
createine "instazoomm2bind"		0;
createine "standardfov"			100;
createine "standardzoomfov"		30;
createine "storezoom"			1;
createine "zoomonrelease"		1;
createine "zoomkey"			"LSHIFT";
createine "zoomchactive"		1;
createine "zoomchs"			25;
createine "nozoomchs"			10;
createine "crosshairid"			4;
createine "ffaautosniperweapons"	0;
createine "quickfistenabled"		0;
createine "zoomscrollin"		5;
createine "zoomscrollout"		10;
createine "zoomscrollinvert"		0;
createine "showhitboxinzoom"		0;
createine "hidehginzoom"		1;

ffabindMouse2 = [
	cycleweapon 3 2 1;
]
	
	
// END OF SETTINGS

alias szfbu $standardzoomfov;
loadcrosshair (concatword "config/crosshair/" (at $chlist $crosshairid)) 0;
loadcrosshair (concatword "config/crosshairfx/" (at $chlist $crosshairid)) 2;
loadcrosshair data/crosshair.png 0;
loadcrosshair "data\hit.png" 2;
crosshairsize $nozoomchs;
alias snipermode 0;
alias prezoomweapon 0;

bind "1" [weapon 6 0 6]
bind "2" [setweapon 1]
bind "3" [setweapon 2]
bind "4" [setweapon 3]
bind "5" [setweapon 4]
bind "6" [setweapon 5]

zoominvel 2000;
zoomfov 60;
zoomoutvel 0;

bind "MOUSE1" [
	attack;
]
specbind "MOUSE1" [
	nextfollow;
	dozoomoff;
]
specbind "MOUSE2" [
	nextfollow -1;
	dozoomoff;
]
specbind "MOUSE3" [
	follow;
]
specbind "MOUSE4" [
	floatspeed (min (+ $floatspeed (div $floatspeed 3)) 1000) ;
	if (< $floatspeed 1000) [echo (concat Speed: $floatspeed)] 
]
specbind "MOUSE5" [
	floatspeed (max (- $floatspeed (div $floatspeed 3)) 10 );
	if (> $floatspeed 10) [echo (concat Speed: $floatspeed)] 
]
createzoombind = [
	bind $zoomkey [
		if (= $zoomonrelease 1) [
			if (= (isinsta) 1) [
				dozoomon;
				onrelease [
					dozoomoff;
					refreshsens;
				]
			] [
				prezoomweapon = (getweapon);
				if (= $ffaautosniperweapons 1) [
					setweapon 0;
					weapon 4 6 2;
				]
				dozoomon;
				onrelease [
					setweapon $prezoomweapon;
					dozoomoff;
					refreshsens;
				]
			]
		] [
			if (= (isinsta) 1) [
				if (= $snipermode 1) [
					dozoomoff;
				] [	
					dozoomon;
				]
			] [
				if (= $snipermode 1) [
					setweapon $prezoomweapon;
					dozoomoff;
				] [
					prezoomweapon = (getweapon);
					if (= $ffaautosniperweapons 1) [
						setweapon 0;
						weapon 4 6 2;
					]
					dozoomon;
				]
			]
		]
		refreshsens;
	]
]
bind "Mouse2" [
	if (= (isinsta) 0) [
		if (= $snipermode 0) [
			if (= $ffam2bind 0) [ ffabindMouse2 ];
			if (= $ffam2bind 1) [ jump ];
		] [
			if (= $storezoom 1) [
				restorezoom;
			] [
				if (= $ffazoomm2bind 0) [ weapon 4 2 6 ];
				if (= $ffazoomm2bind 1) [ ffabindMouse2 ];
				if (= $ffazoomm2bind 2) [ jump ];
				if (= $ffazoomm2bind 3) [ restorezoom ];
				if (= $ffazoomm2bind 4) [ startinstatoggle ];
			]
		]
		if (= $m2bindrepammo 1) [
			repammo;
		]
	] [
		if (= $snipermode 0) [
			if (= $instam2bind 0) [ jump ];
			if (= $instam2bind 1) [ weapon 0 4 0 ];
			if (= $instam2bind 2) [ quickfist ];
		] [
			if (= $storezoom 1) [
				restorezoom;
			] [
				if (= $instazoomm2bind 0) [ jump ];
				if (= $instazoomm2bind 1) [ restorezoom ];
				if (= $instazoomm2bind 2) [ startinstatoggle	];
			]
		]
	]
]
bind "MOUSE4" [
	if (= 1 (isinsta)) [
		if (= $snipermode 1) [
			fovscroll 0;
		] [
			if (< $instam2bind 1) [
				nextweapon -1;
			] [
				fovscroll 0;
			]
		]
	] [
		if (= $snipermode 1) [
			fovscroll 0;
		] [
			if (= $ffascrollmode 0) [
				nextweapon;
			]
			if (= $ffascrollmode 1) [
				cycleweapon (cwgetvalue 0) (cwgetvalue 1) (cwgetvalue 2) (cwgetvalue 3) (cwgetvalue 4)
			]
			if (= $ffascrollmode 2) [
				weapon $wm51 $wm52 $wm53;
			]
			if (= $ffascrollmode 3) [
				setweapon $swm4;
			]
			if (= $ffascrollmode 4) [
				universaldelta 1;
			]
			if (= $ffascrollmode 5) [
				cycleweapon (cuwgetvalue 0) (cuwgetvalue 1) (cuwgetvalue 2) (cuwgetvalue 3) (cuwgetvalue 4)
			]
		]
	]
	if (= $storezoom 1) [
		if (= $snipermode 1) [
			standardzoomfov = $fov;
		]
	]
]
bind "MOUSE5" [
	if (= 1 (isinsta)) [ 
		if (= $snipermode 1) [
			fovscroll 1;
		] [
			if (< $instam2bind 1) [
				nextweapon;
			] [
				fovscroll 1;
			]
		]
	] [
		if (= $snipermode 1) [
			fovscroll 1;
		] [
			if (= $ffascrollmode 0) [
				nextweapon -1;
			]
			if (= $ffascrollmode 1) [
				cycleweapon (cwgetvalue 4) (cwgetvalue 3) (cwgetvalue 2) (cwgetvalue 1) (cwgetvalue 0)
			
			]
			if (= $ffascrollmode 2) [
				weapon $wm41 $wm42 $wm43;
			]
			if (= $ffascrollmode 3) [
				setweapon $swm5;
			]
			if (= $ffascrollmode 4) [
				universaldelta -1;
			]
			if (= $ffascrollmode 5) [
				cycleweapon (cdwgetvalue 0) (cdwgetvalue 1) (cdwgetvalue 2) (cdwgetvalue 3) (cdwgetvalue 4)
			]
		]
	]
	if (= $storezoom 1) [
		if (= $snipermode 1) [
			standardzoomfov = $fov;
		]
	]
]


//editbinds 

editbind "MOUSE1" [ editdrag ]
editbind "MOUSE2" [ editextend ]
editbind "MOUSE3" [ selcorners ]
editbind "MOUSE4" [ universaldelta 1 ]
editbind "MOUSE5" [ universaldelta -1 ]

//function-lib
cdwgetvalue = [
	alias retval (getalias (concatword "cdwid" $arg1));
	if (> $arg1 (- $cdwcounter 1)) [
		retval = 7;
	]
	result $retval;
]
createine = [
	if (strcmp "" (getalias $arg1)) [
		alias $arg1 $arg2;
	]
]
cuwgetvalue = [
	alias retval (getalias (concatword "cuwid" $arg1));
	if (> $arg1 (- $cuwcounter 1)) [
		retval = 7;
	]
	result $retval;
]
cwgetvalue = [
	alias retval (getalias (concatword "cwid" $arg1));
	if (> $arg1 (- $cwcounter 1)) [
		retval = 7;
	]
	result $retval;
]
dozoomoff = [
	if (= $zoomchactive 1) [
		loadcrosshair data/crosshair.png 0;
		loadcrosshair "data\hit.png" 2
	]
	if (= $showhitboxinzoom 1) [
		showboundingbox 0:
	]
	crosshairsize $nozoomchs;
	setfov $standardfov;
	snipermode = 0;
	if (= $hidehginzoom 1) [
		hudgun 1;
	] [
		hudgunfov 65;
	]
]
dozoomon = [
	if (= $zoomchactive 1) [
		loadcrosshair (concatword "config/crosshair/" (at $chlist $crosshairid)) 0;
		loadcrosshair (concatword "config/crosshairfx/" (at $chlist $crosshairid)) 2;
	]
	if (= $showhitboxinzoom 1) [
		showboundingbox 1:
	]
	crosshairsize $zoomchs;
	setfov $standardzoomfov;
	snipermode = 1;
	if (= $hidehginzoom 1) [
		hudgun 0;
	] [
		hudgunfov 35;
	]
]
fovscroll = [
	if (= $arg1 $zoomscrollinvert) [
		setfov (- $fov $zoomscrollin);
	] [
		setfov (+ $fov $zoomscrollout);
	]
]
getscrollmodename = [
	alias retval "";
	if (= $ffascrollmode 0) [
		retval = "nextweapon";
	]
	if (= $ffascrollmode 1) [
		retval = "selfdefined cycle";
	]
	if (= $ffascrollmode 2) [
		retval = "weapon";
	]
	if (= $ffascrollmode 3) [
		retval = "setweapon";
	]
	if (= $ffascrollmode 4) [
		retval = "universaldelta";
	]
	if (= $ffascrollmode 5) [
		retval = "2 cycles";
	]
	result $retval;
]	

isinlist = [
	alias retval 0;
	alias varsel 2;
	while [= 0 (strcmp (getalias (concatword arg $varsel)) "")] [
		list = (getalias (concatword arg $varsel));
		loop i (listlen $list) [
			if (= 1 (strcmp $arg1 (at $list $i ))) [
				retval = 1;
			]
		]
		varsel = (+ $varsel 1);
	]
	result $retval;
]
isinsta = [
	alias gamemode (getmode);
alias rvalue 0;
	if (= $gamemode 3) [
		rvalue = 1;
	] [
	]
	if (= $gamemode 4) [
		rvalue = 1;
	] [
	]
	if (= $gamemode 12) [
		rvalue = 1;
	] [
	]
	if (= $gamemode 14) [
		rvalue = 1;
	] [
	]
	result $rvalue;
]
instatoggle = [
	fullbrightmodels	20
	sleep 50 [
		fullbrightmodels 200
	]
	sleep 100 [
		if (= $instazoomwatch 1) [
			instatoggle;
		] [
		]
	]
]
quickfist = [
	setweapon 0;
	attack;
	onrelease [setweapon 4];
]
refreshsens = [
	if (= $sensscalechange 1) [
		if (= (isinsta) 0) [
			if (= $snipermode 0) [
				sensitivityscale $ssffa;
			] [
				sensitivityscale $ssffazoom;
			]
		] [
			if (= $snipermode 0) [
				sensitivityscale $ssinsta;
			] [
				sensitivityscale $ssinstazoom;
			]
		]
	] [
		sensitivityscale $defaultsensscale;
	]
]
restorezoom = [
	standardzoomfov = $szfbu;
	setfov $standardzoomfov;
]
setfov = [
	fov (min 150 (max 10 $arg1));
	sensitivity (* $fov 3);
]
startinstatoggle = [
	instazoomwatch = 1;
	instatoggle;
	fov $standardfov;
	sensitivity (* $fov 3); 
	instatoggle;
	onrelease [
		instazoomwatch =	0;
		if (= $snipermode 1) [
			setfov $standardzoomfov; 
		] [
			setfov $standardfov; 
		]
	]
]
createzoombind;
setfov $standardfov;
refreshsens;
